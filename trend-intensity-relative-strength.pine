//       __  _________   ________  __
//      / / / / ____/ | / / __ \ \/ /   Henry Oliver
//     / /_/ / __/ /  |/ / /_/ /\  /    https://github.com/henryoliver
//    / __  / /___/ /|  / _, _/ / /     https://twitter.com/henry_s_oliver
//   /_/ /_/_____/_/ |_/_/ |_| /_/
//
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © henry_oliver

//@version=5
indicator(title = "Trend Intensity", shorttitle = "Trend Intensity", overlay = false)

// Constants
const color COLOR_POLAR_NIGHT_NORD2 = color.new(#434C5E, 0)
const color COLOR_POLAR_NIGHT_NORD3 = color.new(#4C566A, 0)
const color COLOR_SNOW_STORM_NORD4 = color.new(#D8DEE9, 0)
const color COLOR_FROST_NORD7 = color.new(#8FBCBB, 0)
const color COLOR_FROST_NORD9 = color.new(#81A1C1, 0)
const color COLOR_FROST_NORD10 = color.new(#5E81AC, 0)
const color COLOR_AURORA_NORD11 = color.new(#BF616A, 0)
const color COLOR_AURORA_NORD14 = color.new(#A3BE8C, 0)

const int RS_LINE_WIDTH = 4
const string RS_BENCH_INDEX = "SPY"

const float TI_BULISH_THRESHOLD_RATIO = 1.05
const float TI_BEARISH_THRESHOLD_RATIO = 0.95
const int TI_LINE_WIDTH = 0
const int TI_SHORT_LENGTH = 7
const int TI_LONG_LENGTH = 65

const float BREAKOUT_RATIO = 1.04
const float BREAKDOWN_RATIO = 0.96

// Style
var bool showRs = input.bool(defval = true, title = "Show", group = "Relative Strength")
var int rsLineWidth = input.int(defval = RS_LINE_WIDTH, title = "Thickness", minval = 1, maxval = 40, step = 1, group="Relative Strength")
var color rsUpColor = input.color(defval = COLOR_FROST_NORD9, title = "Colors", inline = "Colors", group = "Relative Strength")
var color rsDownColor = input.color(defval = COLOR_POLAR_NIGHT_NORD2, title = "▲ ⁃ ▼", inline = "Colors", group = "Relative Strength")
var string rsBenchIndex = input.symbol(defval = RS_BENCH_INDEX, title = "Index", tooltip = "RS Base Index", group = "Relative Strength")

var bool showTi = input.bool(defval = true, title = "Show", group = "Trend Intensity")
var int tiLineWidth = input.int(defval = TI_LINE_WIDTH, title = "Thickness", minval = 0, maxval = 40, step = 1, group="Trend Intensity")
var color tiUpColor = input.color(defval = color.new(COLOR_FROST_NORD7, 90), title = "Colors", inline = "Colors", group = "Trend Intensity")
var color tiDownColor = input.color(defval = color.new(COLOR_AURORA_NORD11, 90), title = "▲ ⁃ ▼", inline = "Colors", group = "Trend Intensity")
var int tiShortLength = input.int(defval = TI_SHORT_LENGTH, title = "MAs (days)", minval = 5, maxval = 200, step = 1, inline = "Moving Averages", group="Trend Intensity")
var int tiLongLength = input.int(defval = TI_LONG_LENGTH, title = "⁃", minval = 5, maxval = 200, step = 1, inline = "Moving Averages", group="Trend Intensity")

var bool showBoBd = input.bool(defval = true, title = "Show", group = "Breakout / Breakdown")
var color breakoutColor = input.color(defval = COLOR_FROST_NORD7, title = "Colors", inline = "Colors", group = "Breakout / Breakdown")
var color breakdownColor = input.color(defval = COLOR_AURORA_NORD11, title = "▲ ⁃ ▼", inline = "Colors", group = "Breakout / Breakdown")
var float breakoutRatio = input.float(defval = BREAKOUT_RATIO, title = "B/O (ratio)", minval = 1, step = 0.01, inline="B/O B/D ratio", group = "Breakout / Breakdown")
var float breakdownRatio = input.float(defval = BREAKDOWN_RATIO, title = "B/D (ratio)", maxval = 1, step = 0.01, inline="B/O B/D ratio", group = "Breakout / Breakdown")

var bool show52w = input.bool(defval = true, title = "Show", group = "52 Weeks H/L")
var color w52HighColor = input.color(defval = color.new(COLOR_FROST_NORD7, 50), title = "Colors", inline = "Colors", group = "52 Weeks H/L")
var color w52LowColor = input.color(defval = color.new(COLOR_AURORA_NORD11, 50), title = "▲ ⁃ ▼", inline = "Colors", group = "52 Weeks H/L")

// Relative Strength
float rsTicker = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = close, ignore_invalid_symbol = true, lookahead = barmerge.lookahead_on)
float rsBench = request.security(symbol = rsBenchIndex, timeframe = "D", expression = close, ignore_invalid_symbol = true, lookahead = barmerge.lookahead_on)
float rsRatio = rsTicker / rsBench
float rsRatioRsi = ta.rsi(rsRatio, 1)
color rsColor = (showRs) ? (rsRatioRsi > 1) ? rsUpColor : (rsRatioRsi < 1) ? rsDownColor : COLOR_POLAR_NIGHT_NORD3 : COLOR_POLAR_NIGHT_NORD3
plot(series = close, title = "Relative Strength", color = rsColor, linewidth = rsLineWidth, style = plot.style_line, editable = false)

// Trend Intensity 65
float tiSmaShort = ta.sma(source = close, length = tiShortLength)
float tiSmaLong = ta.sma(source = close, length = tiLongLength)
float tiRatio = tiSmaShort / tiSmaLong
bool isTiBullish = tiRatio >= TI_BULISH_THRESHOLD_RATIO
bool isTiBearish = tiRatio <= TI_BEARISH_THRESHOLD_RATIO
bool isTi = isTiBullish or isTiBearish
color tiColor = (isTiBullish) ? tiUpColor : (isTiBearish) ? tiDownColor : na
plot(series = (showTi and isTi) ? close : na, title = "Trend Intensity", color = tiColor, linewidth = tiLineWidth, style = plot.style_areabr, editable = false)

// Breakout / Breakdown
float breakMoveRatio = close / open
bool isBreakout = breakMoveRatio >= breakoutRatio
bool isBreakdown = breakMoveRatio <= breakdownRatio
bool hasPriceBroke = isBreakout or isBreakdown
plotchar(series = (showBoBd and hasPriceBroke and isBreakout) ? close : na, title = "Breakout", char = "▲", location = location.abovebar, color = breakoutColor, editable = false, size = size.tiny)
plotchar(series = (showBoBd and hasPriceBroke and isBreakdown) ? close : na, title = "Breakdown", char = "▼", location = location.belowbar, color = breakdownColor, editable = false, size = size.tiny)

// 52 Weeks High / Low
[w52High, w52Low] = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = [ta.highest(high, 252), ta.lowest(low, 252)], ignore_invalid_symbol = true, lookahead = barmerge.lookahead_on)
bool isW52High = high == w52High
bool isW52Low = low == w52Low
bool isW52HighLow = isW52High or isW52Low
color w52Color = (isW52High) ? w52HighColor : (isW52Low) ? w52LowColor : na
plotshape(series = (show52w and isW52HighLow) ? close : na, title = "52 Weeks High / Low", style = shape.circle, location = location.absolute, color = w52Color, editable = false, size = size.small)
