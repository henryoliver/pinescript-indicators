// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © henry_oliver

//@version=6
indicator(title = "Technical View Indicator", shorttitle = "TVI", overlay = true, dynamic_requests = false)

// === CONSTANTS ===
// Calculation Constants
const float THOUSAND = 1e3
const float MILLION = 1e6
const float BILLION = 1e9
const float TRILLION = 1e12

// Nord Theme Color
const color NORD0  = #2E3440
const color NORD1  = #3B4252
const color NORD2  = #434C5E
const color NORD3  = #4C566A
const color NORD4  = #D8DEE9
const color NORD5  = #E5E9F0
const color NORD6  = #ECEFF4
const color NORD7  = #8FBCBB
const color NORD8  = #88C0D0
const color NORD9  = #81A1C1
const color NORD10 = #5E81AC
const color NORD11 = #BF616A
const color NORD12 = #D08770
const color NORD13 = #EBCB8B
const color NORD14 = #A3BE8C
const color NORD15 = #B48EAD

// Input Settings
// Table
var string tablePosition = input.string(defval = position.top_right, title = "Position", 
     options=[position.top_left, position.top_center, position.top_right, 
     position.middle_left, position.middle_center, position.middle_right, 
     position.bottom_left, position.bottom_center, position.bottom_right], group="Table")
var color tableBackgroundColor = input.color(defval = color.new(color = NORD3, transp = 100), title = "Background Color", group = "Table")

var int tableFrameSize = input.int(defval = 0, title = "Frame Size", minval = 0, maxval = 10, group="Table")
var color tableFrameColor = input.color(defval = color.new(color = NORD3, transp = 100), title = "Frame Color", group = "Table")

var int tableBorderSize = input.int(defval = 2, title = "Border Size", minval = 0, maxval = 10, group="Table")
var color tableBorderColor = input.color(defval = color.new(color = NORD3, transp = 100), title = "Border Color", group = "Table")

var bool tableSpacerLeft = input.bool(defval = false, title = "Spacer Left", group = "Table")
var bool tableSpacerRight = input.bool(defval = true, title = "Spacer Right", group = "Table")

// Text
var color textColor = input.color(defval = color.new(color = NORD4, transp = 20), title = "Color", group = "Text")
var int textSize = input.int(defval = 14, title = "Text Size", minval = 8, maxval = 32, group = "Text")
var string textFontFamily = input.string(defval = font.family_default, title = "Font Family", 
     options=[font.family_default, font.family_monospace], group="Text")
var string textHalign = input.string(defval = text.align_left, title = "Horizontal Alignment", 
     options=[text.align_left, text.align_center, text.align_right], group="Text")
var string textValign = input.string(defval = text.align_center, title = "Vertical Alignment", 
     options=[text.align_top, text.align_center, text.align_bottom], group="Text")

// Strength Colors
var color aboveColor = input.color(defval = color.new(color = NORD8, transp = 0), title = "Above Color", group = "Strength")
var color belowColor = input.color(defval = color.new(color = NORD4, transp = 40), title = "Below Color", group = "Strength")

// Technical Indicators
// Market Capitalization
var bool mktCapVisible = input.bool(true, "Visible", group = "Market Capitalization")
var float mktCapBelow = input.float(10, "Sweet Spot (Below $B)", minval = 1, step = 1, 
     tooltip = "Market cap below this value (in billions) indicates higher breakout potential", group = "Market Capitalization")

float tso = request.financial(symbol = syminfo.tickerid, financial_id = "TOTAL_SHARES_OUTSTANDING", period = "FQ")
float mktCap = na(tso) ? na : tso * close
float mktCapBillions = na(mktCap) ? na : mktCap / BILLION

string mktCapText = "CAP " + (na(mktCap) ? "-" : str.tostring(mktCap, format.volume))
color mktCapColor = na(mktCapBillions) ? textColor : 
     mktCapBillions <= mktCapBelow ? aboveColor : textColor
string mktCapTooltip = str.format("Market cap. Below ${0}B is ideal for explosive momentum moves", mktCapBelow)

// Float Shares Outstanding
var bool floatSharesVisible = input.bool(true, "Visible", group = "Float Shares Outstanding")
var float floatSharesAbove = input.float(25, "Ideal (Below M)", minval = 1, step = 5, 
     inline = "FloatShares Strength", group = "Float Shares Outstanding")
var float floatSharesBelow = input.float(100, "▲ - ▼ High (Above M)", minval = 25, step = 10, 
     tooltip = "Float at or below ideal indicates highest breakout potential. Above high threshold suggests slower moves", 
     inline = "FloatShares Strength", group = "Float Shares Outstanding")

float floatShares = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = syminfo.shares_outstanding_float, lookahead = barmerge.lookahead_on)
float floatSharesMillions = na(floatShares) ? na : floatShares / MILLION

string floatSharesText = "FLT " + (na(floatShares) ? "-" : str.tostring(floatShares, format.volume))
color floatSharesColor = na(floatSharesMillions) ? textColor : 
     floatSharesMillions <= floatSharesAbove ? aboveColor : 
     floatSharesMillions >= floatSharesBelow ? belowColor : textColor
string floatSharesTooltip = str.format("Float shares. At or below {0}M is ideal for explosive moves. Above {1}M indicates high float",
     floatSharesAbove, floatSharesBelow)

// Average Daily Volume
var bool volumeVisible = input.bool(true, "Visible", group = "Average Daily Volume")
var int volumePeriod = input.int(20, "Period (Days)", minval = 1, maxval = 100, step = 1, group = "Average Daily Volume")
var float volumeAbove = input.float(500000, "Strong (Above)", minval = 0, step = 100000, 
     inline = "Volume Strength", group = "Average Daily Volume")
var float volumeBelow = input.float(200000, "▲ - ▼ Weak (Below)", minval = 0, step = 100000, 
     tooltip = "Volume above threshold indicates good liquidity. Below threshold suggests low liquidity", 
     inline = "Volume Strength", group = "Average Daily Volume")

float volumeTicker = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = ta.sma(source = volume, length = volumePeriod), lookahead = barmerge.lookahead_on)

string volumeText = "VOL " + (na(volumeTicker) ? "-" : str.tostring(volumeTicker, format.volume))
color volumeColor = volumeTicker >= volumeAbove ? aboveColor : 
     volumeTicker <= volumeBelow ? belowColor : textColor
string volumeTooltip = str.format("{0}-day average daily volume. Above {1, number, #,###} shares indicates strong liquidity",
     volumePeriod, volumeAbove)

// Average Dollar Volume
var bool dollarVolumeVisible = input.bool(true, "Visible", group = "Average Dollar Volume")
var int dollarVolumePeriod = input.int(20, "Period (Days)", minval = 1, maxval = 100, step = 1, group = "Average Dollar Volume")
var float dollarVolumeAbove = input.float(50000000, "Strong (Above)", minval = 0, step = 10000000, 
     inline = "Dollar Volume Strength", group = "Average Dollar Volume")
var float dollarVolumeBelow = input.float(10000000, "▲ - ▼ Weak (Below)", minval = 0, step = 5000000, 
     tooltip = "Dollar volume above threshold indicates institutional interest. Below threshold suggests retail-only stock", 
     inline = "Dollar Volume Strength", group = "Average Dollar Volume")

float dollarVolumeTicker = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = ta.sma(source = volume * close, length = dollarVolumePeriod), lookahead = barmerge.lookahead_on)

string dollarVolumeText = "DVOL " + (na(dollarVolumeTicker) ? "-" : str.tostring(dollarVolumeTicker, format.currency))
color dollarVolumeColor = dollarVolumeTicker >= dollarVolumeAbove ? aboveColor : 
     dollarVolumeTicker <= dollarVolumeBelow ? belowColor : textColor
string dollarVolumeTooltip = str.format("{0}-day average dollar volume. Above ${1, number, #,###} indicates strong institutional participation",

     dollarVolumePeriod, dollarVolumeAbove)

// PE Ratio
var bool peVisible = input.bool(true, "Visible", group = "PE Ratio")
var float peAbove = input.float(40, "High (Above)", minval = 0, step = 5, 
     inline = "PE Strength", group = "PE Ratio")
var float peBelow = input.float(15, "▲ - ▼ Low (Below)", minval = 0, step = 5, 
     tooltip = "P/E above threshold may indicate overvaluation. Below threshold may indicate undervaluation or value opportunity", 
     inline = "PE Strength", group = "PE Ratio")

float peRatio = request.financial(symbol = syminfo.tickerid, financial_id = "PRICE_EARNINGS_FORWARD", period = "FY")
string peFormatted = na(peRatio) ? "-" : str.format("{0, number, #.0}", peRatio)

string peText = "P/E" + peFormatted
color peColor = na(peRatio) ? textColor :
     peRatio >= peAbove ? belowColor : 
     peRatio <= peBelow ? aboveColor : textColor
string peTooltip = str.format("Forward P/E ratio. Above {0} suggests premium valuation. Below {1} suggests value territory",
     peAbove, peBelow)

// PS Ratio
var bool psVisible = input.bool(true, "Visible", group = "PS Ratio")
var float psAbove = input.float(10, "Reasonable (Above)", minval = 0, step = 5, 
     inline = "PS Strength", group = "PS Ratio")
var float psBelow = input.float(50, "▲ - ▼ Overvalued (Above)", minval = 10, step = 5, 
     tooltip = "P/S between reasonable and overvalued range (10-50) is acceptable. Below or above this range may indicate risk", 
     inline = "PS Strength", group = "PS Ratio")

float psRatio = request.financial(symbol = syminfo.tickerid, financial_id = "PRICE_SALES_FORWARD", period = "FY")
string psFormatted = na(psRatio) ? "-" : str.format("{0, number, #.0}", psRatio)

string psText = "P/S" + psFormatted
color psColor = na(psRatio) ? textColor :
     psRatio >= psAbove and psRatio <= psBelow ? aboveColor : belowColor
string psTooltip = str.format("Forward P/S ratio. Between {0} and {1} is reasonable range. Outside this range may signal overvaluation or undervaluation risk",
     psAbove, psBelow)

// Average Daily Range (ADR) %
var bool adrVisible = input.bool(true, "Visible", group = "ADR")
var int adrRange = input.int(20, "Period (Days)", minval = 1, maxval = 100, step = 1, group = "ADR")
var float adrAbove = input.float(0.05, "High Volatility (Above)", minval = 0, maxval = 1, step = 0.01, 
     inline = "ADR Strength", group = "ADR")
var float adrBelow = input.float(0.03, "▲ - ▼ Low Volatility (Below)", minval = 0, maxval = 1, step = 0.01, 
     tooltip = "ADR above threshold indicates high volatility ideal for swing trading. Below threshold suggests choppy or slow price action", 
     inline = "ADR Strength", group = "ADR")

float adrTicker = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = ta.sma(source = (high / low), length = adrRange), lookahead = barmerge.lookahead_on)
float adrRounded = math.round(adrTicker - 1, 4)
string adrFormatted = str.format("{0, number, #.00}%", adrRounded * 100)

string adrText = "ADR" + adrFormatted
color adrColor = adrRounded >= adrAbove ? aboveColor : 
     adrRounded <= adrBelow ? belowColor : textColor
string adrTooltip = str.format("{0}-day average daily range. Above {1, number, #.0}% indicates strong intraday movement ideal for swing trades",
     adrRange, adrAbove * 100)

// Average True Range (ATR) $
var bool atrVisible = input.bool(true, "Visible", group = "ATR")
var int atrRange = input.int(20, "Period (Days)", minval = 1, maxval = 100, step = 1, group = "ATR")
var float atrAbove = input.float(2.0, "High Volatility (Above $)", minval = 0, step = 0.5, 
     tooltip = "ATR above this value indicates high volatility in dollar terms", group = "ATR")

float atrTicker = request.security(symbol = syminfo.tickerid, timeframe = "D", expression = ta.atr(length = atrRange), lookahead = barmerge.lookahead_on)
float atrRounded = math.round(atrTicker, 2)
string atrFormatted = str.format("${0, number, #.00}", atrRounded)

string atrText = "ATR" + atrFormatted
color atrColor = atrRounded >= atrAbove ? aboveColor : textColor
string atrTooltip = str.format("{0}-day average true range. Above ${1, number, #.00} indicates high volatility for position sizing",
     atrRange, atrAbove)

// Relative Price Strength (RPS)
var bool rpsVisible = input.bool(true, "Visible", group = "RPS")
var int rpsPeriod = input.int(63, "Lookback Period (Days)", minval = 20, maxval = 252, step = 1, 
     tooltip = "Period for measuring relative performance (63 days ≈ 3 months)", group = "RPS")
var string rpsIndex = input.symbol("IXIC", title = "Base Index", tooltip = "Benchmark index for relative strength comparison", group = "RPS")
var float rpsAbove = input.float(1.1, "Strong (Above)", minval = 0, step = 0.05, 
     inline = "RPS Strength", group = "RPS")
var float rpsBelow = input.float(0.9, "▲ - ▼ Weak (Below)", minval = 0, step = 0.05, 
     tooltip = "RPS above 1.0 shows outperformance. Below 1.0 shows underperformance vs benchmark", 
     inline = "RPS Strength", group = "RPS")

// Get benchmark data
float rpsBenchmarkCurrent = request.security(symbol = rpsIndex, timeframe = timeframe.period, expression = close, lookahead = barmerge.lookahead_on)
float rpsBenchmarkPast = request.security(symbol = rpsIndex, timeframe = timeframe.period, expression = close[rpsPeriod], lookahead = barmerge.lookahead_on)

// Calculate percentage change for both stock and benchmark
float stockPercentChange = na(close[rpsPeriod]) ? na : close / close[rpsPeriod]
float benchmarkPercentChange = na(rpsBenchmarkPast) ? na : rpsBenchmarkCurrent / rpsBenchmarkPast

// RPS is the ratio of stock performance to benchmark performance
float rpsRounded = na(benchmarkPercentChange) or benchmarkPercentChange == 0 ? na : stockPercentChange / benchmarkPercentChange
string rpsFormatted = na(rpsRounded) ? "-" : str.format("{0, number, #.00}", rpsRounded)


string rpsText = "RPS" + rpsFormatted
color rpsColor = na(rpsRounded) ? textColor :
     rpsRounded >= rpsAbove ? aboveColor : 
     rpsRounded <= rpsBelow ? belowColor : textColor
string rpsTooltip = str.format("Relative price strength vs {0} over {1} days. Above 1.0 = outperforming, below 1.0 = underperforming",
     rpsIndex, rpsPeriod)

// Table Rendering
// Create table only once
var table techTable = table.new(position = tablePosition, columns = 13, rows = 1,
     border_width = tableBorderSize,
     border_color = tableBorderColor,
     frame_width = tableFrameSize,
     frame_color = tableFrameColor)

// Update table on last confirmed historical bar
if barstate.islastconfirmedhistory
    col = 0
    
    // Left spacer
    if tableSpacerLeft
        table.cell(table_id = techTable, column = col, row = 0, text = "   ", bgcolor = tableBackgroundColor, text_size = textSize)
        col += 1

    // Market Cap
    if mktCapVisible
        table.cell(table_id = techTable, column = col, row = 0, text = mktCapText,
             text_color = mktCapColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = mktCapTooltip,
             text_font_family = textFontFamily)
        col += 1
    
    // Float Shares
    if floatSharesVisible
        table.cell(table_id = techTable, column = col, row = 0, text = floatSharesText,
             text_color = floatSharesColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = floatSharesTooltip,
             text_font_family = textFontFamily)
        col += 1

    // Average Daily Volume
    if volumeVisible
        table.cell(table_id = techTable, column = col, row = 0, text = volumeText,
             text_color = volumeColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = volumeTooltip,
             text_font_family = textFontFamily)
        col += 1

    // Average Dollar Volume
    if dollarVolumeVisible
        table.cell(table_id = techTable, column = col, row = 0, text = dollarVolumeText,
             text_color = dollarVolumeColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = dollarVolumeTooltip,
             text_font_family = textFontFamily)
        col += 1

    // PE Ratio
    if peVisible
        table.cell(table_id = techTable, column = col, row = 0, text = peText,
             text_color = peColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = peTooltip,
             text_font_family = textFontFamily)
        col += 1

    // PS Ratio
    if psVisible
        table.cell(table_id = techTable, column = col, row = 0, text = psText,
             text_color = psColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = psTooltip,
             text_font_family = textFontFamily)
        col += 1

    // ADR
    if adrVisible
        table.cell(table_id = techTable, column = col, row = 0, text = adrText,
             text_color = adrColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = adrTooltip,
             text_font_family = textFontFamily)
        col += 1

    // ATR
    if atrVisible
        table.cell(table_id = techTable, column = col, row = 0, text = atrText,
             text_color = atrColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = atrTooltip,
             text_font_family = textFontFamily)
        col += 1

    // RPS
    if rpsVisible
        table.cell(table_id = techTable, column = col, row = 0, text = rpsText,
             text_color = rpsColor,
             text_halign = textHalign,
             text_valign = textValign,
             text_size = textSize,
             bgcolor = tableBackgroundColor,
             tooltip = rpsTooltip,
             text_font_family = textFontFamily)
        col += 1

    // Right spacer
    if tableSpacerRight
        table.cell(table_id = techTable, column = col, row = 0, text = "        ", bgcolor = tableBackgroundColor, text_size = textSize)
        col += 1
